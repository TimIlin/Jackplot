class CharList {
    field CharListItem start;
    field CharListItem end;
    field int length;

    constructor CharList new(){
        let length = 0;
        let start = null;
        let end = null;
        return this;
    }

    method void add(char data){
        var CharListItem prev;
        if (start = null){
            let start = CharListItem.new(data, null, null, 0);
            let end = start;
        }
        else{
            let prev = end;
            let end = CharListItem.new(data, null, end, end.getIndex() + 1);
            do prev.setNext(end);
        }
        let length = length + 1;
        return;
    }

    method void removeAtLast(){
        var CharListItem last;
        if (end = null){
            do Sys.error(18);
        }
        if (end = start){
            let last = end;
            let end = null;
            let start = null;
            do last.dispose();
            return;
        }
        let last = end;
        let end = end.getPrev();
        if (~(end = null)){
            do end.setNext(null);
        }
        do last.dispose();
        let length = length - 1;
        return;
    }

    method void changeAtIndex(int i, char changed_data){
        var CharListItem current;
        var int current_index;

        let current = start;
        let current_index = 0;
        if (i > length | i = length){
            return;
        }
        while(current_index < i){
            let current = current.getNext();
            let current_index = current_index + 1;
        }
        do current.setData(changed_data);
        return;
    }

    method char takeAtIndex(int i){
        var CharListItem current;
        var int current_index;

        let current = start;
        let current_index = 0;
        if (i > length | i = length){
            do Sys.error(15);
        }
        while(current_index < i){
            let current = current.getNext();
            let current_index = current_index + 1;
        }
        return current.getData();
    }

    method void dispose(){
        var CharListItem current;
        let current = start;
        while (~(start = null)){
            let start = start.getNext();
            do current.dispose();
            let current = start;
        }
        do Memory.deAlloc(this);
        return;
    }

    method void print(){
        var CharListItem current;
        let current = start;
        while (~(current = null)){
            do Output.printChar(current.getData());
            do Output.printChar(32);
            let current = current.getNext();
        }
        return;
    }

    method String toString(){
        var String result;
        var CharListItem current;
        let result = String.new(length);
        let current = start;
        while (~(current = null)){
            do result.appendChar(current.getData());
            let current = current.getNext();
        }
        return result;
    }

    method int length(){
        return length;
    }
}